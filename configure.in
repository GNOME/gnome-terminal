AC_INIT(src/terminal.c)

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(gnome-terminal, 2.2.1)

GETTEXT_PACKAGE=gnome-terminal
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")

AC_ARG_WITH(widget, [  --with-widget=[zvt/vte] terminal widget to use])

AM_MAINTAINER_MODE

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AC_PROG_INTLTOOL

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac

  if test "x$enable_ansi" = "xyes"; then
    case " $CFLAGS " in
    *[\ \	]-ansi[\ \	]*) ;;
    *) CFLAGS="$CFLAGS -ansi" ;;
    esac

    case " $CFLAGS " in
    *[\ \	]-pedantic[\ \	]*) ;;
    *) CFLAGS="$CFLAGS -pedantic" ;;
    esac
  fi
fi
changequote([,])dnl

ALL_LINGUAS="am ar be bg ca cs da de el en_GB es et fi fr gl he hu it ja ko lt lv mk mn ms nl no pl pt pt_BR ro ru sk sl sq sv tr uk vi wa zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

AC_PATH_XTRA

## here we get the flags we'll actually use
PKG_CHECK_MODULES(TERM, gtk+-2.0 >= 2.2.0 gconf-2.0 >= 1.1.9 libglade-2.0 libgnomeui-2.0 libstartup-notification-1.0 >= 0.4)

TERM_LIBS="$X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS $TERM_LIBS"
TERM_CFLAGS="$X_CFLAGS $TERM_CFLAGS"
CFLAGS="$TERM_CFLAGS $CFLAGS"

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test x"$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2

# Honor aclocal flags
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

if test x$with_widget = xzvt; then
	PKG_CHECK_MODULES(TERMINAL_WIDGET, libzvt-2.0 >= 1.115.2)
else
        with_widget=vte
	PKG_CHECK_MODULES(TERMINAL_WIDGET, vte >= 0.10.4)
fi

CFLAGS="$TERMINAL_WIDGET_CFLAGS $CFLAGS"

AM_CONDITIONAL(TERMINAL_WIDGET_VTE, test x$with_widget = xvte)

if $PKG_CONFIG --atleast-version 2.0.0 xft && $PKG_CONFIG --atleast-version 1.2.0 pangoxft ; then
   using_xft2=yes
else
   using_xft2=no
fi

AC_OUTPUT([
Makefile
src/Makefile
src/eel/Makefile
src/skey/Makefile
help/Makefile
help/C/Makefile
help/ro/Makefile
po/Makefile.in
])
dnl ==========================================================================

if test x$with_widget = xzvt; then
        echo "*** WARNING WARNING WARNING WARNING WARNING"
        echo "*** Building with Zvt widget instead of VTE"
        echo "*** This is probably a bad idea"
 elif test x$using_xft2 = xno; then
        echo "*** WARNING WARNING WARNING WARNING WARNING"
        echo "*** Building with VTE widget but not Xft2"
        echo "*** This won't be as optimal as fontconfig/Xft2"
fi

echo "
gnome-terminal-$VERSION:

	prefix:                 ${prefix}
	source code location:	${srcdir}
	compiler:		${CC}

        terminal widget:        ${with_widget}
        using fontconfig/Xft2:  ${using_xft2}
"
